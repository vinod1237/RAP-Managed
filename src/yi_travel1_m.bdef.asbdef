managed; //implementation in class zbp_i_travel1_m unique;
strict ( 2 );

define behavior for YI_travel1_M //alias <alias_name>
implementation in class zcl_bp_travel_managed unique
persistent table ytravel_m
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{

  field ( readonly ) TravelId, LastChangedBy, LastChangedAt, CreatedBy, CreatedAt;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  factory action copytravel [1];
  internal action recalcTotPrice;

  validation validateCustomer on save { create; field CustomerId; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateStatus on save { create; field OverallStatus; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateBookingFee on save { create; field BookingFee; }

  determination CalculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  create ( authorization : global );
  update;
  delete;

  association _booking { create ( features : instance ); }
  mapping for ytravel_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }
}

define behavior for YI_booking1_m //alias <alias_name>
implementation in class zcl_bp_Booking_managed unique
persistent table ybooking_m
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
//etag dependent by _travel
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;

  validation validateCustomer on save { create; field CustomerId; }
  validation validateStatus on save { create; field BookingStatus; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateConnection on save { create; field CarrierId, ConnectionId, FlightDate; }
  validation validateFlightPrice on save { create; field FlightPrice; }

  determination CalculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  association _travel;
  association _bookingsuppl { create ( features : instance ); }
  mapping for ybooking_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for Yi_bookSuppl1_m //alias <alias_name>
implementation in class zcl_bp_BookSuppl_managed unique
persistent table ybooksuppl_m
lock dependent by _travel
authorization dependent by _travel
etag master LastChangedAt
//etag dependent by _travel
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId;

  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateSupplement on save { create; field SupplementId; }
  validation validateprice on save { create; field Price; }


  determination CalculateTotalPrice on modify { create; field Price, CurrencyCode; }

  association _travel;
  association _booking;
  mapping for ybooksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}